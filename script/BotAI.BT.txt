tree("Root")
	parallel
		repeat mute tree("Patrol")
		repeat mute tree("Dead")


tree("Patrol")
	fallback
		tree("Flee")
		while not IsHealthLessThan(40.0)
			fallback
				tree("ReturnToLastPositionOfPlayer")
				tree("Attack")
				tree("LookAround")
				tree("Wander")
		
tree("Attack")
	while not IsFleeingFromPlayer()
		sequence
			SeePlayer
			TargetPlayer
			LookAtTarget
			fallback
				while ShotLinedUp
					sequence
						Wait(0.3)
						Fire
				while not ShotLinedUp
					sequence
						SetTargetDestination
						MoveToDestination


tree("Dead")
	while IsHealthLessThan(0.1)
		Explode



tree("Flee")
	sequence
		while IsHealthLessThan(40.0)
			while InDanger(50.0)
				sequence
					SetFleeStatus(true)
					TakeCover
					MoveToDestination


tree("ReturnToLastPositionOfPlayer")
	while not IsHealthLessThan(40.0)
		while not InDanger(50.0)
			while IsFleeingFromPlayer()
				sequence
					SetDestinationToLastKnownPositionOfPlayer
					MoveToDestination
					SetFleeStatus(false)


tree("LookAround")
	while not IsHealthLessThan(40.0)
		while not SeePlayer
			while not IsFleeingFromPlayer()
				sequence
					random
						Turn(90.0)
						Turn(-90.0)
						Turn(145.0)
						Turn(-27.0)
					LookAtTarget
					WaitRandom(2.0,5.0)
					random
						Succeed
						Fail

tree("Wander")
	while not SeePlayer
		while not IsFleeingFromPlayer()
			sequence
				PickRandomDestination
				MoveToDestination
